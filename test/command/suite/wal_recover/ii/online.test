# wal_recover can't use with HTTP because requests via HTTP are always
# processed by workers. Workers never use primary WAL role.
#@require-interface stdio

#@require-feature message_pack

#$GRN_WAL_ROLE=primary

#@disable-logging
cache_limit --max 0
#@enable-logging

table_create Memos TABLE_NO_KEY
column_create Memos content COLUMN_SCALAR Text

table_create Terms TABLE_PAT_KEY ShortText \
  --default_tokenizer TokenNgram \
  --normalizer NormalizerNFKC130
column_create Terms memos_content COLUMN_INDEX|WITH_POSITION Memos content

#@disable-logging
io_flush
#@copy-path #{db_path}.0000103 #{db_path}.0000103.initial
#@copy-path #{db_path}.0000103.c #{db_path}.0000103.c.initial
#@enable-logging

load --table Memos
[
{"content": "hello world"},
{"content": "good-by world"}
]

check --obj Terms.memos_content

select Memos --query content:@hello

#@disable-logging
#@copy-path #{db_path}.0000103.wal #{db_path}.0000103.wal.keep
io_flush
thread_limit 1
database_unmap
#@copy-path #{db_path}.0000103.initial #{db_path}.0000103
#@copy-path #{db_path}.0000103.c.initial #{db_path}.0000103.c
#@copy-path #{db_path}.0000103.wal.keep #{db_path}.0000103.wal
#@enable-logging

select Memos --query content:@hello

#@add-important-log-levels notice
wal_recover
#@remove-important-log-levels notice

select Memos --query content:@hello

check --obj Terms.memos_content
